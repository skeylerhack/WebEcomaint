--InsertCongNhanPBT

IF NOT EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND name = 'InsertCongNhanPBT')
   exec('CREATE PROCEDURE InsertCongNhanPBT AS BEGIN SET NOCOUNT ON; END')
GO
--EXEC InsertCongNhanPBT

ALTER PROC InsertCongNhanPBT
	@MsPBT NVARCHAR(50)
AS

--chi them khi co tu dong cap nhap 
IF NOT EXISTS (SELECT * FROM THONG_TIN_CHUNG WHERE ISNULL(AUTO_CN,0) = 1 AND ISAUTO = 1)
	RETURN

-- Chi them khi loai bao tri la dinh ky
IF NOT EXISTS (SELECT * FROM PHIEU_BAO_TRI T1 INNER JOIN LOAI_BAO_TRI T2 ON T1.MS_LOAI_BT = T2.MS_LOAI_BT INNER JOIN
		HINH_THUC_BAO_TRI T3 ON T2.MS_HT_BT = T3.MS_HT_BT
		WHERE MS_PHIEU_BAO_TRI = @MsPBT AND PHONG_NGUA = 1)
	RETURN
	

DECLARE @MsMay NVARCHAR(100)
DECLARE @MsLBT INT
DECLARE @NgayHT DATETIME


SET @NgayHT = GETDATE()

SELECT @MsMay =  T1.MS_MAY, @MsLBT = T1.MS_LOAI_BT
FROM PHIEU_BAO_TRI T1 INNER JOIN LOAI_BAO_TRI T2 ON T1.MS_LOAI_BT = T2.MS_LOAI_BT INNER JOIN HINH_THUC_BAO_TRI T3 ON T2.MS_HT_BT = T3.MS_HT_BT
WHERE MS_PHIEU_BAO_TRI = @MsPBT AND PHONG_NGUA = 1



DECLARE @NgayMax DATETIME
DECLARE @MsPBTCu NVARCHAR(50)
DECLARE @MsBP NVARCHAR(100)
DECLARE @MsCV INT

Declare  PBTCV CURSOR FOR
	SELECT MS_BO_PHAN, MS_CV FROM PHIEU_BAO_TRI_CONG_VIEC WHERE MS_PHIEU_BAO_TRI = @MsPBT ORDER BY MS_BO_PHAN, MS_CV 
Open PBTCV
FETCH NEXT FROM PBTCV
INTO @MsBP,@MsCV
While @@Fetch_Status = 0
Begin
	SET @NgayMax = NULL
	SELECT @NgayMax = MAX(T1.NGAY_BD_KH)
	FROM         dbo.PHIEU_BAO_TRI AS T1 INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC AS T2 ON T1.MS_PHIEU_BAO_TRI = T2.MS_PHIEU_BAO_TRI INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU AS T3 ON T2.MS_PHIEU_BAO_TRI = T3.MS_PHIEU_BAO_TRI AND 
						  T2.MS_CV = T3.MS_CV AND T2.MS_BO_PHAN = T3.MS_BO_PHAN
	WHERE T2.MS_CV = @MsCV AND T2.MS_BO_PHAN = @MsBP AND T1.MS_MAY =  @MsMay AND T1.MS_LOAI_BT = @MsLBT 

	SET @MsPBTCu = NULL
	SELECT TOP 1 @MsPBTCu =  T1.MS_PHIEU_BAO_TRI
	FROM         dbo.PHIEU_BAO_TRI AS T1 INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC AS T2 ON T1.MS_PHIEU_BAO_TRI = T2.MS_PHIEU_BAO_TRI INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU AS T3 ON T2.MS_PHIEU_BAO_TRI = T3.MS_PHIEU_BAO_TRI AND 
						  T2.MS_CV = T3.MS_CV AND T2.MS_BO_PHAN = T3.MS_BO_PHAN
	WHERE T2.MS_CV = @MsCV AND T2.MS_BO_PHAN = @MsBP AND T1.MS_MAY =  @MsMay AND T1.MS_LOAI_BT = @MsLBT AND NGAY_BD_KH = @NgayMax
	

	INSERT INTO PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU (MS_BO_PHAN, MS_CONG_NHAN,MS_CV,MS_PHIEU_BAO_TRI)
	SELECT T3.MS_BO_PHAN,T3.MS_CONG_NHAN,T3.MS_CV,@MsPBT
	FROM         dbo.PHIEU_BAO_TRI AS T1 INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC AS T2 ON T1.MS_PHIEU_BAO_TRI = T2.MS_PHIEU_BAO_TRI INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU AS T3 ON T2.MS_PHIEU_BAO_TRI = T3.MS_PHIEU_BAO_TRI AND 
						  T2.MS_CV = T3.MS_CV AND T2.MS_BO_PHAN = T3.MS_BO_PHAN
	WHERE T2.MS_CV = @MsCV AND T2.MS_BO_PHAN = @MsBP AND T1.MS_MAY =  @MsMay AND T1.MS_LOAI_BT = @MsLBT AND T1.MS_PHIEU_BAO_TRI = @MsPBTCu 
	AND NOT EXISTS(
	SELECT * FROM PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU A WHERE A.MS_PHIEU_BAO_TRI = @MsPBT AND
		A.MS_BO_PHAN = T3.MS_BO_PHAN AND A.MS_CONG_NHAN = T3.MS_CONG_NHAN
		AND A.MS_CV = T3.MS_CV)

	INSERT INTO PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU_CHI_TIET(DEN_GIO,DEN_NGAY,MS_BO_PHAN,MS_CONG_NHAN,MS_CV,MS_PHIEU_BAO_TRI,NGAY,SO_GIO,TU_GIO,STT)
	SELECT '1900-01-01 17:00:00.000',T1.NGAY_KT_KH,T2.MS_BO_PHAN,MS_CONG_NHAN,T2.MS_CV,@MsPBT,T1.NGAY_BD_KH,T3.SO_GIO,'1900-01-01 08:00:00.000',T3.STT	
	FROM         dbo.PHIEU_BAO_TRI AS T1 INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC AS T2 ON T1.MS_PHIEU_BAO_TRI = T2.MS_PHIEU_BAO_TRI INNER JOIN
						  dbo.PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU_CHI_TIET AS T3 ON T2.MS_PHIEU_BAO_TRI = T3.MS_PHIEU_BAO_TRI AND 
						  T2.MS_CV = T3.MS_CV AND T2.MS_BO_PHAN = T3.MS_BO_PHAN
	WHERE T2.MS_CV = @MsCV AND T2.MS_BO_PHAN = @MsBP AND T1.MS_MAY =  @MsMay AND T1.MS_LOAI_BT = @MsLBT 
	AND T1.MS_PHIEU_BAO_TRI = @MsPBTCu
	AND NOT EXISTS(SELECT * FROM PHIEU_BAO_TRI_CONG_VIEC_NHAN_SU_CHI_TIET A WHERE A.MS_PHIEU_BAO_TRI = @MsPBT AND
		A.MS_BO_PHAN = T3.MS_BO_PHAN AND A.MS_CONG_NHAN = T3.MS_CONG_NHAN AND A.MS_CV = T3.MS_CV AND A.STT = T3.STT)


	FETCH NEXT FROM PBTCV
	INTO  @MsBP,@MsCV
End

Close PBTCV
Deallocate PBTCV