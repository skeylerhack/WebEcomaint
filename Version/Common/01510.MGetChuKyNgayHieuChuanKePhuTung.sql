IF NOT EXISTS (SELECT * FROM sys.objects WHERE type = 'TF' AND name = 'MGetChuKyNgayHieuChuanKePhuTung')
   exec('CREATE FUNCTION  dbo.MGetChuKyNgayHieuChuanKePhuTung() returns  @MAY_NGAY_CUOI TABLE ([MS_MAY] [nvarchar](30) NOT NULL) as begin return end')
GO
--44
--SELECT * FROM [dbo].[MGetChuKyNgayHieuChuanKePhuTung]('01/01/2018','12/31/2018',-1,'ADMIN','-1',-1,-1,'-1','-1',0)  ORDER BY MS_MAY, MS_PT, MS_VI_TRI, MS_DV_TG,CHU_KY,NGAY_KE
ALTER FUNCTION [dbo].[MGetChuKyNgayHieuChuanKePhuTung]
(
	@NgayBD DATETIME,
	@NgayKT DATETIME,
	@LoaiHC INT,
	@UserName NVARCHAR(255),
	@MsNXuong nvarchar(50),
	@NHeThong int,
	@MsBPCP INT =-1,
	@LoaiMay NVARCHAR (20),
	@NhomMay NVARCHAR (20),
	@MLoai bit
)
--@LoaiHC
--1	Hiệu chuẩn nội
--2	Hiệu chuẩn ngoại
--3	Kiểm định
--4	Kiểm tra
--11	Hiệu chuẩn nội lay luon ngay ke ma ngay co ngay hieu chuan gan nhat nam trong gia doan
--12	Hiệu chuẩn ngoại lay luon ngay ke ma ngay co ngay hieu chuan gan nhat nam trong gia doan
--13	Kiểm định lay luon ngay ke ma ngay co ngay hieu chuan gan nhat nam trong gia doan
--14	Kiểm tra lay luon ngay ke ma ngay co ngay hieu chuan gan nhat nam trong gia doan
--select * from LOAI_HIEU_CHUAN
returns  @MAY_NGAY_CUOI TABLE (
	[MS_MAY] [nvarchar](30) NOT NULL,
	[MS_PT] [nvarchar](25) NOT NULL,
	[TEN_PT] NVARCHAR(MAX) NOT NULL,
	[MS_VI_TRI] [nvarchar](50) NOT NULL,
	[NGAY_HC_CUOI] [datetime] NULL,
	[CHU_KY] [smallint] NULL,
	[MS_DV_TG] [int] NULL,
	[SO_NGAY] [int] NULL,
	[SO_NGAY_CK] [int] NULL,
	[NGAY_KE] [datetime] NULL,
	[MS_LOAI_HIEU_CHUAN] INT NULL)
as
begin	
DECLARE @BC INT
IF @LoaiHC > 10 SET @BC = 1 ELSE SET @BC = 0
IF @LoaiHC = 11 SET @LoaiHC = 1
IF @LoaiHC = 12 SET @LoaiHC = 2
IF @LoaiHC = 13 SET @LoaiHC = 3
IF @LoaiHC = 14 SET @LoaiHC = 4

--Lấy dữ liệu theo nha xuong, he thong và tính ngày bảo trì định kỳ cuoi

DECLARE @MAY_USER TABLE (MS_MAY NVARCHAR(30))
INSERT INTO @MAY_USER
SELECT DISTINCT MS_MAY FROM [dbo].[MGetMayUserNgay](@NgayKT,@UserName,@MsNXuong,@NHeThong,@MsBPCP,@LoaiMay,@NhomMay,'-1',0)

IF(YEAR(@NgayKT) >= YEAR(GETDATE()))
BEGIN
INSERT INTO @MAY_NGAY_CUOI
SELECT DISTINCT MS_MAY ,MS_PT,TEN_PT,MS_VI_TRI, NGAY_HC_CUOI , CHU_KY, MS_DV_TG, SO_NGAY, SO_NGAY_CK,
	CASE MS_DV_TG WHEN 1 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (DAY,CHU_KY,NGAY_HC_CUOI) 
						ELSE DATEADD (DAY,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI)	END 
					WHEN 2 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (WEEK,CHU_KY,NGAY_HC_CUOI)
						ELSE DATEADD (WEEK,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI) END 							
					WHEN 3 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (MONTH,CHU_KY,NGAY_HC_CUOI)
							ELSE DATEADD (MONTH,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI) END 
					WHEN 4 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (YEAR,CHU_KY,NGAY_HC_CUOI)
							ELSE DATEADD (YEAR,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI) END END AS NGAY_KE
							,MS_LOAI_HIEU_CHUAN
FROM 
(
	SELECT MS_MAY ,MS_PT,TEN_PT,MS_VI_TRI, NGAY_HC_CUOI , CHU_KY, MS_DV_TG, SO_NGAY, 
	CASE MS_DV_TG WHEN 1 THEN CONVERT(int,SO_NGAY / CHU_KY ) * CHU_KY 
					WHEN 2 THEN CONVERT(int,(SO_NGAY / 7 ) / CHU_KY ) * CHU_KY
					WHEN 3 THEN CONVERT(int,dbo.MGetSoThang(NGAY_HC_CUOI,@NgayBD) / CHU_KY ) * CHU_KY 				
					WHEN 4 THEN CONVERT(int,(SO_NGAY / 12 ) / CHU_KY ) * CHU_KY END AS SO_NGAY_CK,SO_NGAY.MS_LOAI_HIEU_CHUAN
	FROM 
		(
			SELECT     T2.MS_MAY,T1.MS_PT,(CONVERT( NVARCHAR(MAX ),T2.MS_PT +' '+T4.TEN_PT))  AS TEN_PT, T2.MS_VI_TRI, T1.NGAY_HC_CUOI , 
					
					CASE T1.MS_LOAI_HIEU_CHUAN WHEN 1 THEN CHU_KY_HC_NOI 
									WHEN 2 THEN CHU_KY_HC_NGOAI 
									WHEN 3 THEN CHU_KY_KD 
									ELSE CHU_KY_KT END AS CHU_KY,
					--CASE WHEN ISNULL(T2.CHU_KY_HC_NOI,0) = 0 THEN (CASE WHEN ISNULL(T2.CHU_KY_HC_NGOAI,0) = 0 THEN (CASE WHEN ISNULL(T2.CHU_KY_KD,0) = 0 THEN (CASE WHEN ISNULL(T2.CHU_KY_KT,0) = 0 THEN 0 ELSE T2.CHU_KY_KT  END) ELSE T2.CHU_KY_KD  END) ELSE T2.CHU_KY_HC_NGOAI  END) ELSE T2.CHU_KY_HC_NOI  END AS CHU_KY,
					
					MS_DV_TG,
					T1.MS_LOAI_HIEU_CHUAN,
					CASE MS_DV_TG WHEN 1 THEN DATEDIFF (DAY, NGAY_HC_CUOI,@NgayBD) 
											WHEN 2 THEN DATEDIFF (DAY,NGAY_HC_CUOI,@NgayBD)
											WHEN 3 THEN dbo.MGetSoThang(NGAY_HC_CUOI,@NgayBD)
											ELSE  dbo.MGetSoThang(NGAY_HC_CUOI,@NgayBD) END 
					AS SO_NGAY 		
				FROM        ( 
				SELECT T1.MS_MAY, MS_PT, MS_VI_TRI,T1.MS_LOAI_HIEU_CHUAN, MAX(NGAY) AS NGAY_HC_CUOI FROM dbo.HIEU_CHUAN_DHD T1 INNER JOIN 
								@MAY_USER T3 ON T1.MS_MAY = T3.MS_MAY	
								--huong them
								WHERE (MS_LOAI_HIEU_CHUAN = @LoaiHC OR @LoaiHC =-1) 
								GROUP BY T1.MS_MAY, MS_PT, MS_VI_TRI,T1.MS_LOAI_HIEU_CHUAN

							) AS T1 INNER JOIN dbo.CHU_KY_HIEU_CHUAN AS T2 ON T1.MS_MAY = T2.MS_MAY 
								AND T1.MS_PT = T2.MS_PT AND T1.MS_VI_TRI = T2.MS_VI_TRI
								INNER JOIN IC_PHU_TUNG T4 ON T4.MS_PT = T1.MS_PT
				WHERE ISNULL(MS_DV_TG, 0 ) <> 0	
		) SO_NGAY WHERE CHU_KY <> 0
) TINH_SO_NGAY ORDER BY MS_MAY,MS_PT,MS_VI_TRI , MS_DV_TG 
END	
ELSE
BEGIN
INSERT INTO @MAY_NGAY_CUOI
SELECT DISTINCT MS_MAY ,MS_PT,TEN_PT,MS_VI_TRI, NGAY_HC_CUOI , CHU_KY, MS_DV_TG, SO_NGAY, SO_NGAY_CK,
	CASE MS_DV_TG WHEN 1 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (DAY,CHU_KY,NGAY_HC_CUOI) 
						ELSE DATEADD (DAY,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI)	END 
					WHEN 2 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (WEEK,CHU_KY,NGAY_HC_CUOI)
						ELSE DATEADD (WEEK,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI) END 							
					WHEN 3 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (MONTH,CHU_KY,NGAY_HC_CUOI)
							ELSE DATEADD (MONTH,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI) END 
					WHEN 4 THEN CASE WHEN NGAY_HC_CUOI >= @NgayBD THEN DATEADD (YEAR,CHU_KY,NGAY_HC_CUOI)
							ELSE DATEADD (YEAR,CASE ISNULL(SO_NGAY_CK,0) WHEN 0 THEN CHU_KY ELSE ISNULL(SO_NGAY_CK,0) END,NGAY_HC_CUOI) END END AS NGAY_KE,TINH_SO_NGAY.MS_LOAI_HIEU_CHUAN
FROM 
(
	SELECT MS_MAY ,MS_PT,TEN_PT,MS_VI_TRI, NGAY_HC_CUOI , CHU_KY, MS_DV_TG, SO_NGAY, 
	CASE CHU_KY WHEN 0 THEN 0 ELSE
	(CASE MS_DV_TG WHEN 1 THEN CONVERT(int,SO_NGAY / CHU_KY ) * CHU_KY 
					WHEN 2 THEN CONVERT(int,(SO_NGAY / 7 ) / CHU_KY ) * CHU_KY
					WHEN 3 THEN CONVERT(int,dbo.MGetSoThang(NGAY_HC_CUOI,@NgayBD) / CHU_KY ) * CHU_KY 				
					WHEN 4 THEN CONVERT(int,(SO_NGAY / 12 ) / CHU_KY ) * CHU_KY END) END AS SO_NGAY_CK,SO_NGAY.MS_LOAI_HIEU_CHUAN
	FROM 
		(
			SELECT     T2.MS_MAY,T2.MS_PT,(CONVERT( NVARCHAR(MAX ),T2.MS_PT +' '+T3.TEN_PT)) AS TEN_PT, T2.MS_VI_TRI, T1.NGAY_HC_CUOI,T1.MS_LOAI_HIEU_CHUAN,
					CASE T1.MS_LOAI_HIEU_CHUAN WHEN 1 THEN CHU_KY_HC_NOI 
									WHEN 2 THEN CHU_KY_HC_NGOAI 
									WHEN 3 THEN CHU_KY_KD 
									ELSE CHU_KY_KT END AS CHU_KY, MS_DV_TG , 
					CASE MS_DV_TG WHEN 1 THEN DATEDIFF (DAY, NGAY_HC_CUOI,@NgayBD) 
											WHEN 2 THEN DATEDIFF (DAY,NGAY_HC_CUOI,@NgayBD)
											WHEN 3 THEN dbo.MGetSoThang(NGAY_HC_CUOI,@NgayBD)
											ELSE  dbo.MGetSoThang(NGAY_HC_CUOI,@NgayBD) END 
					AS SO_NGAY 		
				FROM        (SELECT T1.MS_MAY, MS_PT, MS_VI_TRI, MAX(NGAY) AS NGAY_HC_CUOI,T1.MS_LOAI_HIEU_CHUAN FROM dbo.HIEU_CHUAN_DHD T1 INNER JOIN 
								@MAY_USER T3 ON T1.MS_MAY = T3.MS_MAY	
								WHERE (MS_LOAI_HIEU_CHUAN = @LoaiHC OR @LoaiHC = -1) and year(@NgayKT) = year(NGAY)
								GROUP BY T1.MS_MAY, MS_PT, MS_VI_TRI,T1.MS_LOAI_HIEU_CHUAN
							) AS T1 INNER JOIN dbo.CHU_KY_HIEU_CHUAN AS T2 ON T1.MS_MAY = T2.MS_MAY 
								AND T1.MS_PT = T2.MS_PT AND T1.MS_VI_TRI = T2.MS_VI_TRI
								INNER JOIN IC_PHU_TUNG T3 ON T3.MS_PT = T1.MS_PT
				WHERE ISNULL(MS_DV_TG, 0 ) <> 0	
		) SO_NGAY WHERE CHU_KY >= 0
) TINH_SO_NGAY ORDER BY MS_MAY,MS_PT,MS_VI_TRI , MS_DV_TG 
END

IF @MLoai = 1
	BEGIN
		DECLARE @MS_MAY NVARCHAR(30),@MS_PT nvarchar (25),@TEN_PT nvarchar (MAX),@MS_VI_TRI nvarchar(50) ,
		@NGAY_CUOI DATETIME ,@NGAY_KE DATETIME, @CHU_KY INT, @MS_DV_TG INT,@SO_NGAY INT,@SO_NGAY_CK INT, @MS_LOAI_HIEU_CHUAN INT
	
		DECLARE BTDK_CURSOR CURSOR FOR
			SELECT DISTINCT MS_MAY,MS_PT,TEN_PT,MS_VI_TRI,MS_DV_TG, CHU_KY,NGAY_HC_CUOI AS NGAY_CUOI,NGAY_KE,SO_NGAY,SO_NGAY_CK,MS_LOAI_HIEU_CHUAN FROM @MAY_NGAY_CUOI 	
			WHERE MS_DV_TG IN (1,2,3,4) AND CHU_KY IS NOT NULL AND CHU_KY > 0
		OPEN BTDK_CURSOR
		FETCH NEXT FROM BTDK_CURSOR
		INTO @MS_MAY, @MS_PT,@TEN_PT,@MS_VI_TRI,@MS_DV_TG,@CHU_KY,@NGAY_CUOI,@NGAY_KE,@SO_NGAY,@SO_NGAY_CK,@MS_LOAI_HIEU_CHUAN
		WHILE @@FETCH_STATUS = 0  
		BEGIN	
			SET @NGAY_KE = (SELECT CASE @MS_DV_TG
						WHEN 1 THEN  DATEADD(DAY,@CHU_KY,@NGAY_KE)
						WHEN 2 THEN DATEADD(DAY,7 * @CHU_KY ,@NGAY_KE)
						WHEN 3 THEN DATEADD(MONTH,@CHU_KY,@NGAY_KE) 
						WHEN 4 THEN DATEADD(YEAR,@CHU_KY,@NGAY_KE)												
						ELSE 
							DATEADD(YEAR,1,@NgayKT)												
						END )
			
			WHILE @NGAY_KE <= @NgayKT	
				BEGIN	
					INSERT INTO @MAY_NGAY_CUOI (MS_MAY,MS_PT,TEN_PT,MS_VI_TRI,NGAY_HC_CUOI,CHU_KY,MS_DV_TG,NGAY_KE,SO_NGAY,SO_NGAY_CK,MS_LOAI_HIEU_CHUAN) 
					VALUES (@MS_MAY,@MS_PT,@TEN_PT,@MS_VI_TRI,@NGAY_CUOI,@CHU_KY,@MS_DV_TG,@NGAY_KE,@SO_NGAY,@SO_NGAY_CK,@MS_LOAI_HIEU_CHUAN) 
					SET @NGAY_KE = (SELECT CASE @MS_DV_TG
						WHEN 1 THEN  DATEADD(DAY,@CHU_KY,@NGAY_KE)
						WHEN 2 THEN DATEADD(DAY,7 * @CHU_KY ,@NGAY_KE)
						WHEN 3 THEN DATEADD(MONTH,@CHU_KY,@NGAY_KE) 
						WHEN 4 THEN DATEADD(YEAR,@CHU_KY,@NGAY_KE)							
						ELSE 
							DATEADD(YEAR,1,@NgayKT)												
						END )
				 END	
			FETCH NEXT FROM BTDK_CURSOR 
			INTO @MS_MAY,@MS_PT,@TEN_PT,@MS_VI_TRI, @MS_DV_TG,@CHU_KY,@NGAY_CUOI,@NGAY_KE,@SO_NGAY,@SO_NGAY_CK,@MS_LOAI_HIEU_CHUAN
		END	
		CLOSE BTDK_CURSOR 
		DEALLOCATE  BTDK_CURSOR 	
	END
	--nếu báo cáo bằng 1 thì xóa những hiệu chuẩn (ngày hiệu chuẩn cuối không nằm trong từ ngày đến ngày)
	IF(YEAR(@NgayBD) >= YEAR(GETDATE()))
	BEGIN
	IF @BC = 1 
		DELETE FROM @MAY_NGAY_CUOI WHERE (NGAY_KE  > @NgayKT) AND (NGAY_HC_CUOI NOT BETWEEN @NgayBD AND @NgayKT)
	ELSE
		DELETE FROM @MAY_NGAY_CUOI WHERE NGAY_KE  > @NgayKT 
	END
	return 
END 	
GO

